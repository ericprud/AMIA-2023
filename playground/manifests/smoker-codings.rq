PREFIX fhir: <http://hl7.org/fhir/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

SELECT ?obsId ?patient ?birthDate ?smokingAssessmentDate ?packYears
       ?smokingYears ?packsPerDay ?dateQuit ?patientAge {

  BIND (NOW() AS ?now) # we'll need this later
# BIND ("2023-10-27T08:17:56.279+02:00"^^xsd:dateTime AS ?now)
  {
    SELECT ?obsId ?patient ?smokingAssessmentDate
           ?packYears ?smokingYears ?packsPerDay ?dateQuit {

      # Observations with code of Tobacco smoking status
      ?obs a fhir:Observation;
        fhir:nodeRole fhir:treeRoot;
        fhir:id [ fhir:v ?obsId ];
        fhir:status [ fhir:v "final" ];
        fhir:code [
          fhir:coding [ rdf:rest*/rdf:first [
            a <http://loinc.org/rdf#72166-2>;
          ] ];
        ];
        fhir:subject [ fhir:link ?patient ];
        fhir:effective [ fhir:v ?smokingAssessmentDate ].
    
      # Ex-smoker (finding)
      ?obs fhir:value [
          fhir:coding [ rdf:rest*/rdf:first [
            a <http://snomed.info/id/8517006> ;
          ] ]
        ].

      # Pack years
      ?obs fhir:component [ rdf:rest*/rdf:first [
        fhir:code [ fhir:coding [ rdf:rest*/rdf:first [
          a <http://snomed.info/id/401201003>
        ] ] ];
        fhir:value [
          fhir:value [ fhir:v ?packYears ];
          fhir:unit [ fhir:v "{PackYears}" ];
          fhir:system [ fhir:v "http://unitsofmeasure.org"^^xsd:anyURI ];
          fhir:code [ fhir:v "{PackYears}" ]
        ];
      ] ] .

      # Smoking years
      ?obs fhir:component [ rdf:rest*/rdf:first [
        fhir:code [ fhir:coding [ rdf:rest*/rdf:first [
          a <http://loinc.org/rdf#88029-4>
        ] ] ];
        fhir:value [
          fhir:value [ fhir:v ?smokingYears ];
          fhir:unit [ fhir:v "Years Used" ];
          fhir:system [ fhir:v "http://unitsofmeasure.org"^^xsd:anyURI ];
          fhir:code [ fhir:v "a" ]
        ];
      ] ].

      # Packs per day
      ?obs fhir:component [ rdf:rest*/rdf:first [
        fhir:code [ fhir:coding [ rdf:rest*/rdf:first [
          a <http://loinc.org/rdf#8663-7>
        ] ] ];
        fhir:value [
          fhir:value [ fhir:v ?packsPerDay ];
          fhir:unit [ fhir:v "Packs/Day" ];
          fhir:system [ fhir:v "http://snomed.info/sct"^^xsd:anyURI ];
          fhir:code [ fhir:v "228963008" ]
        ];
      ] ].

      # Date quit
      OPTIONAL {
        ?obs fhir:component [ rdf:rest*/rdf:first [
          fhir:code [ fhir:coding [ rdf:rest*/rdf:first [
            a <http://loinc.org/rdf#74010-0>
          ] ] ];
          fhir:value [ fhir:v ?dateQuit ];
        ] ].
      }

    }
    ORDER BY DESC(?smokingAssessmentDate)
    LIMIT 1
  }
  # Assessment within last year
  BIND( day(?now) - day(?smokingAssessmentDate)
        + 365.25/12*(month(?now) - month(?smokingAssessmentDate)
        + 12*(year(?now) - year(?smokingAssessmentDate)))
       AS ?smokingAssessmentAge)
  FILTER ( ?smokingAssessmentAge < 365.25 )

  # Patient in age range
  ?patient fhir:birthDate [ fhir:v ?birthDate ] .
  BIND( (day(?now) - day(?birthDate)
         + 365.25/12*(month(?now) - month(?birthDate)
         + 12*(year(?now) - year(?birthDate)))
        )/365.25
       AS ?patientAge)
  FILTER (?patientAge > 50 && ?patientAge < 100)

  # ... Diagnosis, Service, ServiceRequest
}
